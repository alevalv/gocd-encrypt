# Maintainer: Alejandro Valdes <alejandrovaldes at live dot com>

pkgname=gocd-encrypt
pkgver=0.1.1
pkgrel=1
pkgdesc="CLI to encrypt Strings using a remote gocd server"
arch=('x86_64')
url="https://github.com/alevalv/gocd-encrypt"
license=('GPLv2')
makedepends=('rust' 'cargo')
source=("https://github.com/alevalv/gocd-encrypt/archive/${pkgver}.zip")
sha512sums=('bd41149939381d23c9ebf6710d05a89971b2e5056ec497334abb22705b961b0674db5e37ee945854a8f63e744d3dad5394dac81213a712a955850a14f9eb462e')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cargo fetch --locked
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cargo build --release --locked
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dm 755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 755 "target/_${pkgname}" "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  install -Dm 755 "target/${pkgname}.bash" "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
}
